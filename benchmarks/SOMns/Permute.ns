(* Copyright (c) 2001-2016 see AUTHORS.md file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*)
class PermuteSuite usingPlatform: platform andHarness: harness = (
| private Array     = platform kernel Array.
  private Benchmark = harness Benchmark.
|
) (
  class Permute = Benchmark (
  | private count ::= 0.
    private v     ::= nil.
  |)(

    public benchmark = (
      count:: 0.
      v::     Array new: 6 withAll: 0.
      self permute: 6.
      ^ count
    )

    public verifyResult: result = (
      ^ 8660 = result
    )

    private permute: n = (
      count:: count + 1.
      (n <> 0)
        ifTrue: [
          self permute: n - 1.
          n downTo: 1 do: [ :i |
            self swap: n with: i.
            self permute: n - 1.
            self swap: n with: i ] ]
    )

    private swap: i with: j = (
      | tmp |
      tmp:: v at: i.
      v at: i put: (v at: j).
      v at: j put: tmp
    )
  )

  public newInstance = ( ^ Permute new )
)