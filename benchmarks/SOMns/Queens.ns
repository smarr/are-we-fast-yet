(* Copyright (c) 2001-2016 see AUTHORS.md file
Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*)
class QueensSuite usingPlatform: platform andHarness: harness = (
| private Array     = platform kernel Array.
  private Benchmark = harness Benchmark.
|
)(
  class Queens = Benchmark (
  | private freeMaxs ::= nil.
    private freeRows ::= nil.
    private freeMins ::= nil.
    private queenRows ::= nil.
  |)(
    public benchmark = (
      | result |
      result:: true.
      1 to: 10 do: [ :i | result:: result and: self queens ].
      ^ result
    )

    public verifyResult: result = (
      ^ result
    )

    private queens = (
      freeRows::  Array new:  8 withAll: true.
      freeMaxs::  Array new: 16 withAll: true.
      freeMins::  Array new: 16 withAll: true.
      queenRows:: Array new:  8 withAll: -1.
      ^ self placeQueen: 1
    )

    private placeQueen: c = (
      1 to: 8 do: [ :r |
        (self row: r column: c)
          ifTrue: [
            queenRows at: r put: c.
            self row: r column: c put: false.
            (c = 8) ifTrue: [ ^ true ].
            (self placeQueen: c + 1) ifTrue: [ ^ true ].
            self row: r column: c put: true ] ].
      ^ false
    )

    private row: r column: c = (
      ^(freeRows at: r) && (freeMaxs at: c + r) && (freeMins at: c - r + 8)
    )

    row: r column: c put: v = (
      freeRows at: r         put: v.
      freeMaxs at: c + r     put: v.
      freeMins at: c - r + 8 put: v.
    )
  )

  public newInstance = ( ^ Queens new )
)
