(* Copyright (c) 2001-2016 see AUTHORS.md file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*)
class ListSuite usingPlatform: platform andHarness: harness = (
| private Benchmark = harness Benchmark. |
) (
  class List = Benchmark ()(

    benchmark = ( | result |
      result:: self
                  talkWithX: (self makeList: 15)
                  withY:     (self makeList: 10)
                  withZ:     (self makeList: 6).
      ^ result length
    )

    verifyResult: result = (
      ^ 10 = result
    )

    makeList: length = (
      (length = 0)
        ifTrue: [ ^ nil ]
        ifFalse: [
          | e |
          e:: ListElement new: length.
          e next: (self makeList: (length - 1)).
          ^ e ]
    )

    isShorter: x than: y = (
      | xTail yTail |

      xTail:: x. yTail:: y.
      [ yTail isNil ]
        whileFalse: [
          xTail isNil ifTrue: [ ^ true ].
          xTail:: xTail next.
          yTail:: yTail next ].

      ^ false
    )

    talkWithX: x withY: y withZ: z = (
      (self isShorter: y than: x)
        ifTrue: [
          ^ (self
              talkWithX: (self talkWithX: x next withY: y withZ: z)
              withY: (self talkWithX: y next withY: z withZ: x)
              withZ: (self talkWithX: z next withY: x withZ: y)) ]
        ifFalse: [ ^ z ].
    )
  )

  class ListElement new: n = (
    | public val  ::= n.
      public next ::= nil.
    |
  ) (
    public length = ( next isNil ifTrue: [ ^ 1 ] ifFalse: [ ^ (1 + next length) ] )
  )

  public newInstance = ( ^ List new )
)
